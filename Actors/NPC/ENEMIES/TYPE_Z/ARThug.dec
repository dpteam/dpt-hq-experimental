ACTOR ARThug
{
	YScale 0.3
	XScale 0.35
	Radius 20
	Height 56
	Health 90
	Speed 11
	PainChance 255
	SeeSound "Sights/Male07"
	PainSound "Pains/Male07"
	DeathSound "Deaths/Male07"
	DropItem "WWTAk47" 84
	DropItem "762mmAmmo" 84
	DamageFactor "EnemyBullet", 0.3
	DamageFactor "EnemyGrenade", 0.3
	DamageFactor "PlayerPistol", 0.4
	DamageFactor "PlayerUzi", 0.4
	DamageFactor "PlayerShotgun", 0.4
	DamageFactor "PlayerAK", 0.9
	DamageFactor "PlayerM60", 0.9
	DamageFactor "PlayerMagnum", 0.7
	DamageFactor "PlayerMelee", 2.0
	Obituary "%o was hit fast and sharp by a commando."
	MONSTER
	+FLOORCLIP
	+MISSILEMORE
	States
	{
	Spawn:
		ATHG A 1 A_Look
		Loop
	See:
		ATHG BBCCDDEE 4 A_Chase
		Loop
	Missile:
		ATHG F 0 A_JumpIfInventory("AK47Loaded",12,"Reload")
		ATHG FGG 4 A_FaceTarget
		ATHG G 0 A_GiveInventory("AK47Loaded",1)
		ATHG G 0 A_PlayWeaponSound("AK47/EnemyFire")
		ATHG G 0 A_SpawnItemEx("RifleCasing",24,-32,40,8,2,4,random(-55,-80),SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		ATHG G 2 BRIGHT A_CustomMissile("EnemyARTracer", 39, 0, -4)
		ATHG G 3 A_FaceTarget
		ATHG G 0 A_GiveInventory("AK47Loaded",1)
		ATHG G 0 A_PlayWeaponSound("AK47/EnemyFire")
		ATHG G 0 A_SpawnItemEx("RifleCasing",24,-32,40,8,2,4,random(-55,-80),SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		ATHG G 2 BRIGHT A_CustomMissile("EnemyARTracer", 39, 0, 0)
		ATHG G 3 A_FaceTarget
		ATHG G 0 A_GiveInventory("AK47Loaded",1)
		ATHG G 0 A_PlayWeaponSound("AK47/EnemyFire")
		ATHG G 0 A_SpawnItemEx("RifleCasing",24,-32,40,8,2,4,random(-55,-80),SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		ATHG G 2 BRIGHT A_CustomMissile("EnemyARTracer", 39, 0, 4)
		ATHG H 3
		ATHG F 4
		Goto See
	Reload:
		ATHG F 3 A_PlayWeaponSound("AK47/Clipout")
		ATHG F 10
		ATHG H 10 A_PlayWeaponSound("AK47/Clipin")
		ATHG F 6
		ATHG H 4 A_PlayWeaponSound("AK47/Cock")
		ATHG F 5 A_TakeInventory("AK47Loaded",30)
		Goto See
	Pain.PlayerPistol:
	Pain.PlayerUzi:
	Pain.PlayerShotgun:
		ATHG IIIIIIIIII 0 A_SpawnItemEx("BulletIneffectiveSpark",0,0,32,random(-2,2),random(-2,2),random(3,6),random(0.0,360),SXF_NOCHECKPOSITION)
		ATHG I 3 A_PlaySound("Bullet/HitMetal")
		Goto See
	Pain.PlayerElectricGrenade:
	Pain.PlayerTaser:
	Pain.EnemyTaser:
		ATHG I 0
		Goto Pain
	Pain:
		ATHG I 3 A_Pain
		Goto See
	Death:
		ATHG J 3 A_Scream
		ATHG KL 3
		ATHG M 3 A_NoBlocking
		ATHG NO 3
		ATHG P 3 A_PlaySound("Misc/Bodyfall")
		ATHG Q -1
		Stop
	}
}

Actor ARThug_F : ARThug 22223
{
	+FRIENDLY
}