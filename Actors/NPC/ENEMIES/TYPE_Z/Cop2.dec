ACTOR Cop2
{
	XScale 0.35
	YScale 0.3
	Radius 20
	Height 56
	Health 30
	Speed 12
	PainChance 255
	SeeSound "Sights/Male02"
	PainSound "Pains/Male02"
	DeathSound "Deaths/Male04"
	DropItem "9mmAmmo"
	DamageFactor "EnemyBullet", 0.3
	DamageFactor "EnemyGrenade", 0.3
	DamageFactor "PlayerPistol", 0.5
	DamageFactor "PlayerUzi", 0.5
	DamageFactor "PlayerMelee", 2.0
	Obituary "%o was declared D.O.A. by a rookie cop."
	MONSTER
	+FLOORCLIP
	States
	{
	Spawn:
		COP3 A 1 A_Look
		Loop
	See:
		COP3 BCDE 6 A_Chase
		Loop
	Missile:
		COP3 FGH 5 A_FaceTarget
		COP3 F 0 A_JumpIfInventory("BerettaLoaded", 9, "Reload")
		COP3 G 0 A_PlayWeaponSound("Beretta/EnemyFire")
		COP3 G 0 A_SpawnItemEx("BulletCasing",24,-24,40,8,2,4,random(-55,-80),SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		COP3 H 2 BRIGHT A_CustomMissile("EnemyPistolTracer", 42, 0, random(-2,2))
		COP3 H 6 A_GiveInventory("BerettaLoaded", 1)
		COP3 F 0 A_JumpIfInventory("BerettaLoaded", 9, "Reload")
		COP3 H 0 A_PlayWeaponSound("Beretta/EnemyFire")
		COP3 G 0 A_SpawnItemEx("BulletCasing",24,-24,40,8,2,4,random(-55,-80),SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		COP3 H 2 BRIGHT A_CustomMissile("EnemyPistolTracer", 42, 0, random(-2,2))
		COP3 H 6 A_GiveInventory("BerettaLoaded", 1)
		COP3 F 0 A_JumpIfInventory("BerettaLoaded", 9, "Reload")
		COP3 H 0 A_PlayWeaponSound("Beretta/EnemyFire")
		COP3 G 0 A_SpawnItemEx("BulletCasing",24,-24,40,8,2,4,random(-55,-80),SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		COP3 H 2 BRIGHT A_CustomMissile("EnemyPistolTracer", 42, 0, random(-2,2))
		COP3 H 3 A_GiveInventory("BerettaLoaded", 1)
		COP3 G 5
		COP3 F 4
		Goto See
	Reload:
		COP3 G 5 A_PlayWeaponSound("Beretta/Reload")
		COP3 F 10
		COP3 A 5 A_TakeInventory("BerettaLoaded", 9)
		Goto Missile
	Pain.PlayerPistol:
	Pain.PlayerUzi:
		COP3 IIIIIIIIII 0 A_SpawnItemEx("BulletIneffectiveSpark",0,0,32,random(-2,2),random(-2,2),random(3,6),random(0,360),SXF_NOCHECKPOSITION)
		COP3 I 3 A_PlaySound("Bullet/HitMetal")
		Goto See
	Pain.PlayerElectricGrenade:
	Pain.PlayerTaser:
	Pain.EnemyTaser:
		COP3 I 0
		Goto Pain
	Pain:
		COP3 I 3 A_Pain
		Goto See
	Death:
		COP3 J 3 A_Scream
		COP3 KL 3
		COP3 M 3 A_PlaySound("Misc/Bodyfall")
		COP3 N 3 A_NoBlocking
		COP3 OP 3
		COP3 Q -1
		Stop
	}
}

Actor Cop2_F : Cop2 22217
{
	+FRIENDLY
}