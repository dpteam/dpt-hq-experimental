ACTOR MagnumThug
{
	YScale 0.35
	XScale 0.4
	Radius 20
	Height 56
	Health 50
	Speed 16
	PainChance 255
	SeeSound "Sights/Male06"
	PainSound "Pains/Male06"
	DeathSound "Deaths/Male06"
	DropItem "9mmAmmoLarge"
	DamageFactor "EnemyBullet", 0.3
	DamageFactor "EnemyGrenade", 0.3
	DamageFactor "PlayerPistol", 0.5
	DamageFactor "PlayerUzi", 0.5
	DamageFactor "PlayerMelee", 2.0
	Obituary "%o's death was an Executive Decision."
	MONSTER
	+FLOORCLIP
	+MISSILEMORE
	States
	{
	Spawn:
		MTHG A 1 A_Look
		Loop
	See:
		MTHG BCDE 6 A_Chase
		Loop
	Missile:
		MTHG F 10 A_FaceTarget
		MTHG G 0 A_PlayWeaponSound("Beretta/EnemyFire")
		COP3 G 0 A_SpawnItemEx("BulletCasing",24,-24,40,8,2,4,random(-55,-80),SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		MTHG G 2 BRIGHT A_CustomMissile("EnemyPistolTracer", 48, 0, random(-1.0,1.0))
		MTHG HF 5 A_FaceTarget
		MTHG G 0 A_PlayWeaponSound("Beretta/EnemyFire")
		COP3 G 0 A_SpawnItemEx("BulletCasing",24,-24,40,8,2,4,random(-55,-80),SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		MTHG G 2 BRIGHT A_CustomMissile("EnemyPistolTracer", 48, 0, random(-1.0,1.0))
		MTHG H 5
		MTHG F 4
		Goto See
	Pain.PlayerPistol:
	Pain.PlayerUzi:
		MTHG IIIIIIIIII 0 A_SpawnItemEx("BulletIneffectiveSpark",0,0,32,random(-2,2),random(-2,2),random(3,6),random(0.0,360),SXF_NOCHECKPOSITION)
		MTHG I 3 A_PlaySound("Bullet/HitMetal")
		Goto See
	Pain.PlayerElectricGrenade:
	Pain.PlayerTaser:
	Pain.EnemyTaser:
		MTHG I 0
		Goto Pain
	Pain:
		MTHG I 3 A_Pain
		Goto See
	Death:
		MTHG J 3 A_Scream
		MTHG KLM 3
		MTHG N 3 A_NoBlocking
		MTHG O 3 A_PlaySound("Misc/Bodyfall")
		MTHG P -1
		Stop
	}
}

Actor MagnumThug_F : MagnumThug 22211
{
	+FRIENDLY
}