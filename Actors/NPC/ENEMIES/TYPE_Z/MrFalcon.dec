ACTOR MrFalconReload : Inventory
{
	Inventory.MaxAmount 6
}

ACTOR MrFalcon
{
	//SpawnID 7
	YScale 0.35
	XScale 0.4
	Radius 20
	Height 56
	Health 1000
	Speed 20
	PainChance 255
	PainChance "PlayerPistol", 32
	PainChance "PlayerUzi", 48
	PainChance "PlayerM60", 32
	PainChance "PlayerTaser", 40
	PainChance "PlayerGrenadeFire", 16
	PainChance "Fire", 16
	PainChance "EnemyTaser", 40
	Mass 9999
	SeeSound "Sights/MrFalcon"
	PainSound "Pains/MrFalcon"
	DeathSound "UberDeaths/MrFalcon"
	DamageFactor "EnemyBullet", 0.0
	DamageFactor "EnemyGrenade", 0.0
	DamageFactor "PlayerPistol", 0.4
	DamageFactor "PlayerUzi", 0.4
	DamageFactor "PlayerShotgun", 0.4
	DamageFactor "PlayerAK", 0.9
	DamageFactor "PlayerM60", 0.9
	DamageFactor "PlayerMagnum", 0.7
	DamageFactor "PlayerMelee", 2.0
	DamageFactor "PlayerGrenade", 0.5
	DamageFactor "PlayerAKGrenade", 0.5
	DamageFactor "PlayerMGL", 0.5
	DamageFactor "PlayerGrenadeFire", 0.2
	DamageFactor "Fire", 0.2
	Obituary "%o was yippie-ki-yay'd by Mr. Falcon."
	MONSTER
	+BOSS
	+FLOORCLIP
	+MISSILEMORE
	+MISSILEEVENMORE
	+DONTMORPH
	States
	{
	Spawn:
		LTHG A 1 A_Look
		Loop
	See:
		LTHG BBCCDDEE 3 A_Chase
		Loop
	Missile:
		LTHG F 0 A_JumpIfInventory("MrFalconReload",6,"Reload")
		LTHG F 0 A_PlaySound("Firing/MrFalcon", CHAN_AUTO)
		LTHG F 8 A_PlaySound("MGL/Close", CHAN_AUTO, 1.0, 0, 0.4)
		LTHG G 8 A_FaceTarget
		LTHG G 0 A_PlaySound("MGL/Fire", CHAN_AUTO, 1.0, 0, 0.4)
		LTHG G 2 BRIGHT A_CustomMissile("EnemyLauncherGrenade", 46, 0, random(-3,3), 0, random(-3, 3))
		LTHG G 10 A_GiveInventory("MrFalconReload",1)
		Goto See
	Reload:
		LTHG F 0 A_PlaySound("Reloading/MrFalcon", CHAN_VOICE)
		LTHG F 10 A_PlayWeaponSound("MGL/Open")
		LTHG G 0 A_TakeInventory("MrFalconReload",1)
		LTHG G 7 A_PlayWeaponSound("MGL/Load")
		LTHG F 5
		LTHG G 0 A_TakeInventory("MrFalconReload",1)
		LTHG G 7 A_PlayWeaponSound("MGL/Load")
		LTHG F 5
		LTHG G 0 A_TakeInventory("MrFalconReload",1)
		LTHG G 7 A_PlayWeaponSound("MGL/Load")
		LTHG F 5
		LTHG G 0 A_TakeInventory("MrFalconReload",1)
		LTHG G 7 A_PlayWeaponSound("MGL/Load")
		LTHG F 5
		LTHG G 0 A_TakeInventory("MrFalconReload",1)
		LTHG G 7 A_PlayWeaponSound("MGL/Load")
		LTHG F 5
		LTHG G 0 A_TakeInventory("MrFalconReload",1)
		LTHG G 7 A_PlayWeaponSound("MGL/Load")
		LTHG F 5
		LTHG A 5 A_PlayWeaponSound("MGL/Close")
		Goto See
	Pain.PlayerElectricGrenade:
	Pain.PlayerTaser:
	Pain.EnemyTaser:
		LTHG H 0
		Goto Pain
	Pain:
		LTHG H 0 A_JumpIfHealthLower(200,"HeavyPain")
		LTHG H 3 A_Pain
		Goto See
	HeavyPain:
		LTHG H 3 A_PlaySound("HeavyPain/MrFalcon", CHAN_VOICE)
		Goto See
	Death:
		LTHG H 20 A_Scream
		LTHG HHHHHHHHHHHHHHHHHHHH 3 A_SpawnItemEx("DIExplosion",random(-20.0,20.0),random(-20.0,20.0),random(0,32.0),0,0,0,SXF_NOCHECKPOSITION)
		LTHG IJ 3 A_SpawnItemEx("DIExplosion",random(-20.0,20.0),random(-20.0,20.0),random(0,32.0),0,0,0,SXF_NOCHECKPOSITION)
		LTHG K 3 A_SpawnItemEx("DIExplosion",random(-20.0,20.0),random(-20.0,20.0),random(0,32.0),0,0,0,SXF_NOCHECKPOSITION)
		LTHG LMN 3 A_SpawnItemEx("DIExplosion",random(-20.0,20.0),random(-20.0,20.0),random(0,32.0),0,0,0,SXF_NOCHECKPOSITION)
		LTHG O 3 A_PlaySound("Misc/Bodyfall")
		LTHG P 3
		LTHG P -1 A_BossDeath
		Stop
	}
}

Actor MrFalcon_F : MrFalcon 22210
{
	+FRIENDLY
}